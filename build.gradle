plugins {
    id 'net.minecraftforge.gradleutils' version '2.+'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'me.champeau.jmh' version '0.6.5'
    id 'org.javamodularity.moduleplugin' version '1.8.7' apply false
}
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'org.javamodularity.moduleplugin'

group 'net.minecraftforge'

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

changelog {
    fromTag "1.0.0"
}

version = gradleutils.getTagOffsetVersion()
logger.lifecycle('Version: ' + version)

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

test {
    useJUnitPlatform()
    forkEvery 1
}

sourceSets {
    testJars
}

ext.sharedManifest = manifest {
    attributes(['Specification-Title': 'eventbus',
                'Specification-Vendor': 'Forge',
                'Specification-Version': '1', // Currently version 1 of the eventbus specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${System.getenv("BUILD_NUMBER")?:0}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor' :'forge',
                'Implementation-Timestamp': java.time.Instant.now().toString(),
                'Git-Commit': gradleutils.gitInfo.abbreviatedId,
                'Git-Branch': gradleutils.gitInfo.branch,
                'Build-Number': "${System.getenv('BUILD_NUMBER')?:0}" ],
            'net/minecraftforge/eventbus/service/')
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task testsJar(type: Jar) {
    archiveClassifier = 'testsjar'
    from sourceSets.testJars.output
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


repositories {
    mavenLocal()
    maven {
        name = 'forge'
        url = 'https://maven.minecraftforge.net/'
    }
}

configurations.jmh {
    resolutionStrategy {
        force ('net.sf.jopt-simple:jopt-simple:5.0.4')
    }
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.+')
    testImplementation('org.powermock:powermock-core:2.0.+')
    testImplementation('cpw.mods:modlauncher:9.0.+')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.7.+')
    testImplementation('org.apache.logging.log4j:log4j-core:2.14.1')
    testImplementation('com.lmax:disruptor:3.4.2')
    testImplementation(sourceSets.testJars.runtimeClasspath)
    testJarsImplementation(sourceSets.main.output)
    implementation('org.ow2.asm:asm:9.1')
    implementation('org.ow2.asm:asm-commons:9.1')
    implementation('org.ow2.asm:asm-tree:9.1')
    implementation('org.apache.logging.log4j:log4j-api:2.14.1')
    implementation('cpw.mods:modlauncher:9.0.+')
    compileOnly('org.jetbrains:annotations:21.0.1')
    api('net.jodah:typetools:0.8.+')
    jmhImplementation('cpw.mods:modlauncher:9.0.+')
    jmhImplementation('org.powermock:powermock-core:2.0.+')
    jmhImplementation('org.ow2.asm:asm:9.1')
    jmhImplementation('org.ow2.asm:asm-tree:9.1')
    jmhImplementation('org.ow2.asm:asm-commons:9.1')
    jmh('org.ow2.asm:asm:9.1')
}

jmh {
    jvmArgs = ['-Djmh.separateClasspathJAR=true']
    includes = [ 'net.minecraftforge.eventbus.benchmarks.EventBusBenchmark' ]
    benchmarkMode = ['avgt' ]
    profilers = [ 'stack' ]
    timeOnIteration = '5s'
    warmup = '5s'
    warmupIterations = 3
    iterations = 3
    fork = 3
    timeUnit = 'ns'
}

tasks['jmh'].dependsOn(clean)

artifacts {
    archives jar
    archives sourcesJar
    archives testsJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'Event Bus'
                description = 'High performance Event Bus library'
                url = 'https://github.com/MinecraftForge/eventbus'
                scm {
                    url = 'https://github.com/MinecraftForge/eventbus'
                    connection = 'scm:git:git://github.com/MinecraftForge/eventbus.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/eventbus.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/eventbus/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}